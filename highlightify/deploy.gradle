apply plugin: 'maven'
apply plugin: 'signing'

project.group = 'com.noveogroup.android'
project.version = '0.1'

boolean isReleaseBuild, isSnapshotBuild, isLocalBuild
String sonatypeRepositoryUrl
if (hasProperty('release')) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
} else if (hasProperty('shapshot')) {
    isSnapshotBuild = true
    project.version += '-SNAPSHOT'
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    isLocalBuild = true
}

// task sourcesJar(type: Jar, dependsOn: classes) {
//     classifier = 'sources'
//     from sourceSets.main.allSource
// }

// task javadocJar(type: Jar, dependsOn: javadoc) {
//     classifier = 'javadoc'
//     from javadoc.destinationDir
// }

// artifacts {
//     archives jar
//     archives sourcesJar
//     archives javadocJar
// }

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

signing {
    required { isReleaseBuild }
    sign configurations.archives
}

uploadArchives {
    repositories {
        if (isLocalBuild) {
            mavenLocal()
        } else {
            mavenDeployer {
                if (isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }
                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                pom {
                    artifactId = 'highlightify'
                    project {
                        name 'Highlightify'
                        description 'Add pressed state to text color and drawables from runtime'
                        url 'https://github.com/noveogroup/Highlightify'
                        licenses {
                            license {
                                name 'The MIT License (MIT)'
                                url 'http://opensource.org/licenses/MIT'
                                distribution 'repo'
                            }
                        }
                        scm {
                            url 'https://github.com/noveogroup/Highlightify'
                            connection 'scm:git@github.com:noveogroup/Highlightify.git'
                            developerConnection 'scm:git@github.com:noveogroup/Highlightify.git'
                        }
                        developers {
                            developer {
                                name 'Roman Zhilich'
                                url 'https://github.com/RomanZhilich'
                                roles {
                                    role 'architect'
                                    role 'developer'
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
